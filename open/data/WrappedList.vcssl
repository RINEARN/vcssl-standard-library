encode UTF-8;

/* --------------------------------------------------
 * Open VCSSL API Library ( www.vcssl.org/api/open )
 * --------------------------------------------------
 * open.data.WrappedList 1.0.1
 * 内部ラッピング保持式リストデータ構造ライブラリ
 *
 * Spec: ---
 * Note: ---
 *
 * License: --- ( Public Domain )
 * -------------------------------------------------- */


import open.data.DataWrapper;


/**
 * リストとして扱う構造体です。
 * @author 松井文宏
 */
public struct WrappedList<Type>{
	int size = 0;
	DataWrapper<Type> data[1];
}



/**
 * 使用中のサイズを返します。
 * @author 松井文宏
 */
public int size<Type>( WrappedList<Type> &list ){
	return list.size;
}



/**
 * 要素を全て削除します。
 * @author 松井文宏
 */
public void clear<Type>( WrappedList<Type> &list ){
	alloc[0] list.data;
	alloc[1] list.data;
	list.size = 0;
}



/**
 * リストの末尾に要素を追加します。
 * @author 松井文宏
 */
public void add<Type>( WrappedList<Type> &list, Type value ){
	setProperLength<Type>(list);
	list.data[ list.size++ ] = wrap<Type>(value);
}



/**
 * 特定インデックスの要素を取得します。
 * @author 松井文宏
 */
public Type get<Type>( WrappedList<Type> &list, int index ){
	if(list.size <= index){
		error("WrappedListのインデックスが領域外を指定しました。");
		exit();
	}
	return unwrap<Type>(list.data[ index ]);
}



/**
 * 特定インデックスの要素を代入します。
 * @author 松井文宏
 */
public void set<Type>( WrappedList<Type> &list, int index, Type element ){
	if(list.size <= index){
		error("WrappedListのインデックスが領域外を指定しました。");
		exit();
	}
	list.data[ index ] = wrap<Type>(element);
}



/**
 * 特定インデックスに要素を挿入します。
 * @author 松井文宏
 */
public Type insert<Type>( WrappedList<Type> &list, int index, Type value ){
	if(list.size <= index){
		error("WrappedListのインデックスが領域外を指定しました。");
		exit();
	}
	int n = list.size;
	list.size++;
	setProperLength<Type>(list);
	for(int i=n-1; index<=i; i--){
		list.data[ i+1 ] = list.data[ i ];
	}
	list.data[ index ] = wrap<Type>(value);
}



/**
 * 特定インデックスの要素を削除します。
 * @author 松井文宏
 */
public Type removeAt<Type>( WrappedList<Type> &list, int index ){
	if(list.size <= index){
		error("Listのインデックスが領域外を指定しました。");
		exit();
	}
	int n = list.size;
	for(int i=index; i<n-1; i++){
		list.data[ i ] = list.data[ i+1 ];
	}
	list.size--;
	setProperLength<Type>(list);
}



/**
 * 指定された要素を削除します。
 * @author 松井文宏
 */
public void remove<Type>( WrappedList<Type> &list, Type element ){
	int index = indexOf<Type>(list, element);
	if(0 <= index){
		removeAt<Type>(list, index);
	}
}



/**
 * 指定された要素を全て削除します。
 * @author 松井文宏
 */
public void removeAll<Type>( WrappedList<Type> &list, Type element ){
	int index = indexOf<Type>(list, element);
	while( 0 <= (index = indexOf<Type>(list, element)) ){
		removeAt<Type>(list, index);
	}
}



/**
 * 指定された要素を含んでいるか判定します。
 * @author 松井文宏
 */
public bool contains<Type>( WrappedList<Type> &list, Type element ){
	int n = list.size;
	DataWrapper<Type> wrappedElement = wrap<Type>(element);
	for(int i=0; i<n; i++){
		if(list.data[i] == wrappedElement){
			return true;
		}
	}
	return false;
}



/**
 * 指定された要素を検索し、存在すればそのインデックスを、存在しなければ-1を返します。
 * @author 松井文宏
 */
public int indexOf<Type>( WrappedList<Type> &list, Type element ){
	int n = list.size;
	DataWrapper<Type> wrappedElement = wrap<Type>(element);
	for(int i=0; i<n; i++){
		if(list.data[i] == wrappedElement){
			return i;
		}
	}
	return -1;
}



/**
 * 指定された要素を検索し、存在すればそのインデックスを、存在しなければ-1を返します。
 * @author 松井文宏
 */
public int indexOf<Type>( WrappedList<Type> &list, Type element, int from ){
	int n = list.size;
	if(n <= from){
		error("WrappedListの検索開始インデックスが領域外を指定しました。");
	}
	for(int i=from; i<n; i++){
		if( list.data[i] == wrap<Type>(element) ){
			return i;
		}
	}
	return -1;
}



/**
 * 全要素の順序を反転させます。
 * @author 松井文宏
 */
public void reverse<Type>( WrappedList<Type> &list ){
	int n = list.size;
	DataWrapper<Type> rev[n];
	for(int i=0; i<n; i++){
		rev[i] = list.data[n-1-i];
	}
	list.data = rev;
}



/**
 * 使用中のサイズに適切な配列バッファ長を設定します。
 * @author 松井文宏
 */
private void setProperLength<Type>( WrappedList<Type> &list ){
	int zero = 0;
	int length = length( list.data, zero );
	int properLength = getProperLength(length, list.size);
	if(properLength != length){
		alloc[ properLength ] list.data;
	}
}



/**
 * 使用中の要素数に最適なリストバッファ長を返します。
 * @author 松井文宏
 */
private int getProperLength(int length, int size){
	if( length == 0 ){
		return 1;
	}else if( length <= size ){
		while( length <= size ){
			length *= 2;
		}
		return length;
	}else if( size < length/2 ){
		while( size < length ){
			length /= 2;
		}
		return length;
	}else{
		return length;
	}
}

